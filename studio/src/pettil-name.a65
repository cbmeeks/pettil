; pettil-name.a65
#echo .       pettil-name.a65           Text parser

;--------------------------------------------------------------
#if 0
name=SKIP
stack=( delim -- offset )
tags=nosymbol
`addr1` is the starting address in the input stream where up
to `size1` bytes are skipped *WHILE* they match `delim`.  Returns
`addr2` as the address of the first non-matching character and
`count2` as the remaining number of bytes in the buffer.
#endif
skip
    lda #$F0                    ; BEQ opcode
    .byt $2C                    ; BIT abs
    ;fall through
;--------------------------------------------------------------
#if 0
name=SCAN
stack=( delim -- offset )
tags=self-modifying,nosymbol
Pass through the input stream until `delim` either matches (or
for `skip`, does not match).  Leave the `offset` into the line
buffer on the stack.
#endif
scan
    lda #$D0                    ; BNE opcode
    sta skipscan03              ; #selfmodifying
    ldy userin                  ; `in`
    dey
skipscan01
    iny
    cpy userspan                ; Carry true at end of line
    bcc skipscan02
    lda #0                      ; oh, we done
    sta userspan
    beq skipscan04
skipscan02
    lda (ACC+2*N6),y            ; get char from buffer
    eor tos                     ; compare to character we seek
skipscan03
    bvc skipscan01              ; dummy opcode, BEQ(skip) BNE(scan)
skipscan04
    sty userin                  ; found char !, move `in`
    tya
    jmp put0a

;--------------------------------------------------------------
#if 0
name=?REFILL
stack=( -- )
tags=interpreter,nosymbol
|Three possible states coming in|<|<|h
|1|span is 0||
|2|||
|3|||

* 'force refill' check first -- exit to outer caller ( of INTERPRET )
* 'end of line/buffer' check next
* 'processing' do nothing else, exit


* TIB Terminal Input Buffer
** aka 'user I/O from the PET keyboard'
* PETSCII screens
** extracted from virtual memory buffer as screen `packets`
** `interpret` processes one 40 or 80 byte `logical line` at a time
** line-wrap is treated as whitespace
* Forth blocks
** 16x64 ASCII blocks //(future support planned)//
* SEQ ASCII file //(future support planned)//
** continuous text stream from SEQ file //(future support planned)//

```
: ?refill   ( -- )
    span @ ?exit
    query ;
```
#endif
#include "align.i65"
_qrefill
    jsr enter
#include "page.i65"
    .word span
#include "page.i65"
    .word fetch
#include "page.i65"
    .word _qexit
#include "page.i65"
    .word _query
#include "pass.i65"
qrefill01
    .word exit

;--------------------------------------------------------------
#if 0
name=NAME
stack=( char -- nfa | false )
tags=outer,extra
Parse the next `word` from the `input stream`.  Return `false` if the
`input stream` is exhausted, or the `nfa` of the found word, built
directly within the input stream.  Advance `in` past the parsed `word`.

Leading delimiters are ignored.   If the delimiter is not found the value of
IN is the size of the input stream.  If the delimiter is found IN is
 adjusted to indicate the offset to the character following
 the delimiter.  `span` is unmodified.

In PETTIL `name` replaces the function of Forth-83 //required// reserved
word `word`, which is //omitted//.

As much of the `ugly` as I could for the `blk` conspiracy goes in here.
Isolating conspiracy support in as few words as possible will hopefully
reduce overall system complexity and present clear transitions between the
phases of the outer interpreter.

```
: name   ( nfa -- xfa flag )
     ?refill 'stream swap >n6 2drop
     dup skip dup>r 1- <n6 +
     swap scan r> - >n8 dup
     if  over c! in 1+!
     dup \ something for nip
     then nip ;
```
#endif
#include "align.i65"
_name
    jsr enter
#include "page.i65"
    .word _qrefill
#include "page.i65"
    .word _tickstream
#include "page.i65"
    .word swap
#include "pass.i65"
    .word tozp
    .byt ACC+2*N6               ; set up buffer pointer `n6`
#include "page.i65"
    .word twodrop
#include "page.i65"
    .word dup
#include "page.i65"
    .word skip                  ; start of name
#include "page.i65"
    .word duptor
#include "page.i65"
    .word oneminus
#include "pass.i65"
    .word zpfrom
    .byt ACC+2*N6               ; buf
#include "page.i65"
    .word plus
#include "page.i65"
    .word swap
#include "page.i65"
    .word scan                  ; end of name (and/or buffer)
#include "page.i65"
    .word rfrom
#include "page.i65"
    .word minus
#include "pass.i65"
    .word tozp
    .byt ACC+2*N8               ; name.len
#include "page.i65"
    .word dup
#include "pass.i65"
    .word qbranch
    .byt <(name01-*+1)
#include "page.i65"
    .word over
#include "page.i65"
    .word cstore                ; prepend name in buffer with name.len
#include "page.i65"
    .word in
#include "page.i65"
    .word oneplusstore          ; move `in` past the space that stopped us
#include "page.i65"
    .word dup                   ; something for `nip` to consume
#include "page.i65"
name01
    .word nip
#include "pass.i65"
    .word exit

#print (*-skip)
